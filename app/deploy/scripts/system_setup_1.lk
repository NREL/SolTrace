
/* ******************************************
     configure the sun.  
   ****************************************** */

Sun.useldh = true;
Sun.lat = 40;
Sun.day = 180;
Sun.hour = 17;
Sun.shape = 'g';
Sun.sigma = 3.1;
sunopt( Sun );

/*
// example configures a user defined sunshape with an array of [angle,intensity] datapoints
MySun = [ [0, 1],
			[0.4, 0.9989],
			[0.8, 0.884] ];

sunopt( {'userdata'=MySun, 'shape'='u'} );
*/


/* ******************************************
     configure an optical property data set
   ****************************************** */
   
clearoptics(); // remove any optical properties currently defined
addoptic('concen_1');
opticopt( 'concen_1', 1, {'refl'=0.8, 'trans'=0.9, 'errslope'=0.71, 'errspec'=0.14 } );

// example: print optic property data
opt = opticopt('concen_1', 1 );
outln('concen_1=', opt);



/* ******************************************
     add a stage with a single flat element
   ****************************************** */

clearstages(); // clear the system
addstage( 'stage 1' );

// we need to set the current stage to be active so we can add elements to it
activestage( 'stage 1' ); 

addelement(); // adds element to current stage
elementopt( 0, {'en'=true, 'x'=0, 'y'=0, 'z'=0 ,
	'ax'=0, 'ay'=1, 'az'=0, 
	'zrot'=0, 'aper'=['r',6.2,6.83,0,0,0,0,0,0],
	'surf'=['f',0,0,0,0,0,0,0,0],
	'optic'='concen_1'} );
	


/* ******************************************
    start the trace
   ****************************************** */
traceopt({ 'rays'=2000 } );
trace();
	

outln('final ray hits: ' + rayhits( 0, 0, true ));

