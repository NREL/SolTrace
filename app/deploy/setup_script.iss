; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
PrivilegesRequired=none
AppId={{F74ECEAF-DC07-41BA-AADA-255956903EF1}
AppName=SolTrace

; 64-bit installer extensions - remove if an issue
;ArchitecturesAllowed=x86 x64 ia64
;ArchitecturesInstallIn64BitMode=x64 ia64

; 32-bit and 64-bit installer
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
; On all other architectures it will install in "32-bit mode".
ArchitecturesInstallIn64BitMode=x64
; Note: We don't set ProcessorsAllowed because we want this
; installation to run on all architectures (including Itanium,
; since it's capable of running 32-bit code too).



; UPDATE THESE TO MATCH THE VERSION
AppVerName=SolTrace 3.3.0
DefaultDirName={sd}\SolTrace\3.3.0

AppPublisher=National Renewable Energy Laboratory
AppPublisherURL=http://www.nrel.gov/csp/soltrace
AppSupportURL=http://www.nrel.gov/csp/soltrace
AppUpdatesURL=http://www.nrel.gov/csp/soltrace
DefaultGroupName=SolTrace
OutputDir=.
OutputBaseFilename=soltrace-install
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes
InfoBeforeFile=setup_disclaimer.rtf
UsePreviousAppDir=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]

Source: "help/*"; DestDir: "{app}/help"; Excludes: ".svn,*.map"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "images/*"; DestDir: "{app}/images"; Excludes: ".svn,*.map"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "samples/*"; DestDir: "{app}/samples"; Excludes: ".svn,*.map"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "scripts/*"; DestDir: "{app}/scripts"; Excludes: ".svn,*.map"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "api/*"; DestDir: "{app}/api"; Excludes: ".svn,*.map"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "x64/ssleay32.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/msvcr120.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/msvcp120.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/dbghelp.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/libssh2.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/libeay32.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/libcurl.dll"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
Source: "x64/soltrace.exe"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs
;Source: "x64/soltrace.pdb"; DestDir: "{app}/x64"; Excludes: ".svn,*.map";  Flags: ignoreversion recursesubdirs createallsubdirs

; NOTE: Don't use "Flags: ignoreversion" on any shared system files
[Icons]
Name: "{group}\SolTrace"; Filename: "{app}\x64\soltrace.exe"  ; Check: Is64BitInstallMode
;Name: "{group}\{cm:ProgramOnTheWeb,SolTrace}"; Filename: "http://soltrace.nrel.gov"
;Name: "{group}\{cm:UninstallProgram,SolTrace}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\SolTrace (x64)"; Filename: "{app}\x64\soltrace.exe"; Tasks: desktopicon; Check: Is64BitInstallMode

[Registry]
; 12/4/08 - delete left over entry to prevent issue reported by Paul - opening with earlier version when new verions installed
Root: HKCR; Subkey: "Applications\soltrace.exe"; ValueType: none; ValueName: ; ValueData: SolTrace; Flags: deletekey; Check: IsAdminLoggedOn

; 12/4/08 for admin privileges -see Documentation\InnoSetup\AdminPrivileges
Root: HKCR; Subkey: ".sam"; ValueType: string; ValueName: ; ValueData: "NREL.SolTrace"; Flags: uninsdeletevalue; Check: IsAdminLoggedOn
Root: HKCR; Subkey: "NREL.SolTrace"; ValueType: string; ValueName: ; ValueData: "SolTrace Project File"; Flags: uninsdeletekey; Check: IsAdminLoggedOn         
Root: HKCR; Subkey: "NREL.SolTrace\DefaultIcon"; ValueType: string; ValueName: ; ValueData: "{app}\win32\soltrace.exe,0"; Check: IsAdminLoggedOn and not Is64BitInstallMode
Root: HKCR; Subkey: "NREL.SolTrace\shell\open\command"; ValueType: string; ValueName: ; ValueData: """{app}\win32\soltrace.exe"" ""%1"""; Check: IsAdminLoggedOn and not Is64BitInstallMode
Root: HKCR; Subkey: "NREL.SolTrace\DefaultIcon"; ValueType: string; ValueName: ; ValueData: "{app}\x64\soltrace.exe,0"; Check: IsAdminLoggedOn  and Is64BitInstallMode
Root: HKCR; Subkey: "NREL.SolTrace\shell\open\command"; ValueType: string; ValueName: ; ValueData: """{app}\x64\soltrace.exe"" ""%1"""; Check: IsAdminLoggedOn  and Is64BitInstallMode

; 12/4/08 for non-admin privileges -see Documentation\InnoSetup\AdminPrivileges
Root: HKCU; Subkey: "Software\Classes\.sam"; ValueType: string; ValueName: ; ValueData: "NREL.SolTrace"; Flags: uninsdeletevalue; Check: not IsAdminLoggedOn
Root: HKCU; Subkey: "Software\Classes\NREL.SolTrace"; ValueType: string; ValueName: ; ValueData: "SolTrace Project File"; Flags: uninsdeletekey; Check: not IsAdminLoggedOn            
Root: HKCU; Subkey: "Software\Classes\NREL.SolTrace\DefaultIcon"; ValueType: string; ValueName: ; ValueData: "{app}\x64\soltrace.exe,0"; Check: not IsAdminLoggedOn  and Is64BitInstallMode
Root: HKCU; Subkey: "Software\Classes\NREL.SolTrace\shell\open\command"; ValueType: string; ValueName: ; ValueData: """{app}\x64\soltrace.exe"" ""%1"""; Check: not IsAdminLoggedOn  and Is64BitInstallMode

[Run]
Filename: "{app}\x64\soltrace.exe"; Flags: postinstall skipifsilent unchecked; Description: "{cm:LaunchProgram,SolTrace}"; Check: Is64BitInstallMode

; added 9/19/07 to check for running instances on install and uninstall

[Code]

function InitializeUninstall(): boolean;

begin
   MsgBox('Please close any running instances of SolTrace before continuing.', mbConfirmation, MB_OK);
   Result := true;
end;

// 12/08/08 - added to select appropriate installation path that is writeable by the user
function IsRegularUser(): Boolean;
begin
  Result := not (IsAdminLoggedOn or IsPowerUserLoggedOn);
end;

// 4/14/09 - modify to install in localappdata always - avoids UAC issue reported by Paul
function DefDirRoot(Param: String): String;
begin
//  if IsRegularUser then
    Result := ExpandConstant('{localappdata}')
//  else
//    Result := ExpandConstant('{pf}')
end;


